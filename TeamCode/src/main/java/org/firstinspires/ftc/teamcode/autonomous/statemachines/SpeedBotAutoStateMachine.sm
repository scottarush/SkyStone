%class AutonomousController
%package org.firstinspires.ftc.teamcode.autonomous
// http://smc.sourceforge.net/
%start SpeedBotAutoStateMachine::Idle

%map SpeedBotAutoStateMachine
%%
    Idle
    {
        evStartDriveToStones jump(GetStones::Start) { }
        evStartDragFoundation jump(DragFoundation::Start) { }
        evStartBridgePark jump(BridgePark::Start) { }
    }
%%

    // StateMachine for GetStones sequence
%map GetStones
%%
    /*
    * drives forward close enough to be able to locate a Skystone with the camera
    */
    Start
        Entry {
            openHand();
            // start drive forward.
            // linearDrive(22d)
            // Hack it for States just drive up to the quarry.  checkStoneRecognition
            // will automatically trigger evNoStoneFound
            linearDrive(28d);
        }
    {
        evDriveComplete nil { checkStoneRecognition(); }
        evDriveTimeout nil {  checkStoneRecognition(); }
        // Triggered by checkStoneRecognition if we found a Skystone
        evSkystoneFound StrafeToStone { strafeToSkystone(10d,2000); }
        // Triggered by checkStoneRecognition if we found a Skystone
        evStoneFound StrafeToStone { strafeToStone(10d,2000); }
        // Triggered by checkStoneRecognition if no stone was found
        evNoStoneFound DriveForwardToStone {   }
    }

    StrafeToStone
        Entry{
         }
    {
        evDriveComplete DriveForwardToStone {}
        evDriveTimeout DriveForwardToStone {}
    }

    DriveForwardToStone
        Entry{
            linearDriveSlow(8d);
        }
    {
        evDriveComplete GrabStone {}
        // Give it a try anyway
        evDriveTimeout GrabStone {}
    }
    /*
    * Stop after drive forward and check to see if we are at foundation.
    */
    GrabStone
        Entry{
            // close the hook and wait for it to stop so that we grab the block
            closeHand();
            startTimer(1000);
         }
    {
        evTimeout BackupToBeginDrag { }
    }
    /*
    * Backup to begin the drag
    */
    BackupToBeginDrag
        Entry{
            linearDrive(-20d);
         }
    {
        evDriveComplete [ctxt.isBlueAlliance() == true] RotateTowardBridge {
            // Rotate CCW toward the bridge
            rotate(+90);
        }
        evDriveComplete [ctxt.isBlueAlliance() == false] RotateTowardBridge {
            // Rotate CW toward the bridege
            rotate(-90);
        }
        // Try it anyway
        evDriveTimeout [ctxt.isBlueAlliance() == true] RotateTowardBridge {
            // Rotate CCW toward the bridge
            rotate(+90);
        }
        // Try it anyway
        evDriveTimeout [ctxt.isBlueAlliance() == false] RotateTowardBridge {
            // Rotate CW toward the bridege
            rotate(-90);
        }
    }

    /*
    * Rotates toward the bridge and then starts the linear drive to the bridge
    */
    RotateTowardBridge
    {
        evRotationComplete nil  { linearDriveBugUnblocker(); }
        // Try it anyway even if rotation is off.
        evRotationTimeout nil  { linearDriveBugUnblocker(); }
        evDriveComplete DragStone { }
        evDriveTimeout DragStone { }
    }
    /*
    * Drags the stone back under the bridge
    */
    DragStone
        Entry {
            linearDrive(62d);
        }
    {
        evDriveComplete ReleaseStone {}
        evDriveTimeout ReleaseStone {}
    }
    ReleaseStone
        Entry {
            openHand();
            linearDriveSlow(-6d);
        }
    {
        evDriveComplete BackupToBridgeLine {  }
        evDriveTimeout BackupToBridgeLine { }
    }

    /*
    *
    */
    BackupToBridgeLine
        Entry {
            //  backup to the line
            closeHand();
            linearDriveSlow(-12d);
        }
    {
       evDriveComplete [ctxt.isBlueAlliance() == true] RotateToWall {
            rotate(-90);
        }
        evDriveComplete [ctxt.isBlueAlliance() == false] RotateToWall {
            rotate(+90);
        }
        evDriveTimeout [ctxt.isBlueAlliance() == true] RotateToWall {
            rotate(-90);
        }
        evDriveTimeout [ctxt.isBlueAlliance() == false] RotateToWall {
            rotate(+90);
        }
    }
    RotateToWall
    {
        evRotationComplete nil { linearDriveBugUnblocker(); }
        evRotationTimeout nil { linearDriveBugUnblocker(); }
        evDriveComplete BackupToWall {  }
        evDriveTimeout BackupToWall {  }
    }

    BackupToWall
        Entry{
        linearDriveSlow(-15d);
    }
    {
        evDriveComplete Complete { }
        evDriveTimeout Complete { }
    }
    /*
    * Final state
    */
    Complete
        Entry{
            stop();
        }
    {

    }
%%
// StateMachine for DragFoundation sequence
%map DragFoundation
%%
    /*
    * drive forward 26 inches.  This should push the foundation by 2 inches
    */
    Start
        Entry {
            openHook();
            closeHand();
            // Raise crane by 4 inches to get clearance for dragging
            moveCrane(4d);
            startTimer(1000);
        }
    {
        evTimeout DriveToFoundation { }
    }

    DriveToFoundation
        Entry{
            // start drive forward
            linearDrive(32d);
    }
    {
       evDriveComplete GrabFoundation {  }
        // Give it a college try on timeout, but we may not have hooked it
        evDriveTimeout GrabFoundation { }
    }

    GrabFoundation
        Entry {
            closeHook();
            startTimer(2000);
        }
    {
        evTimeout DragFoundation {  }
    }

    DragFoundation
        Entry {
            linearDrive(-26d);
        }
    {
        evDriveComplete [ctxt.isBlueAlliance() == true] RotateFoundation {
            // Rotate CCW toward the blue line
            rotate(+55);
         }
        evDriveComplete [ctxt.isBlueAlliance() == false] RotateFoundation {
            // Rotate CW toward the red line
            rotate(-55);
        }
        evDriveTimeout [ctxt.isBlueAlliance() == true] RotateFoundation {
            // Rotate CCW toward the blue line
            rotate(+55);
        }
        evDriveTimeout [ctxt.isBlueAlliance() == false] RotateFoundation {
            // Rotate CW toward the red line
            rotate(-55);
        }
    }
    RotateFoundation
    {
        // May not have rotated far enough release and try to correct rotation for backup
        evRotationTimeout ReleaseAndCorrectRotation { }
        // Rotation was good so go to release
        evRotationComplete ReleaseFoundation { }
    }

    ReleaseFoundation
        Entry {
            openHook();
            startTimer(1500);
        }
    {
        evTimeout [ctxt.isBlueAlliance() == true] RotateToBackup {
            // Rotate CCW to line up backwards to the skybridge
            rotate(+40);
        }
        evTimeout [ctxt.isBlueAlliance() == false] RotateToBackup {
            // Rotate CW to line up backwards to the skybridge
            rotate(-40);
        }
    }
    /**
    * Corrects rotation error if we weren't able to rotate the foundation by the full amount
    */
    ReleaseAndCorrectRotation
    Entry{
        openHook();
        startTimer(1500);
    }
    {
        evTimeout [ctxt.isBlueAlliance() == true] RotateToBackup {
            // Rotate CCW to line up backwards to the skybridge adding the error from not being
            // able to rotate the foundation as far
            rotate(+55+(40-ctxt.getLastRotationAngle()));
        }
        evTimeout [ctxt.isBlueAlliance() == false] RotateToBackup {
            // Rotate CW to line up backwards to the skybridge
            rotate(-55+(-40-ctxt.getLastRotationAngle()));
        }
    }

    RotateToBackup
    {
        evRotationComplete nil { linearDriveBugUnblocker(); }
        // May not have completed but give it a try anyway
        evRotationTimeout nil {  linearDriveBugUnblocker(); }
        evDriveComplete LowerCrane { }
        evDriveTimeout LowerCrane { }
     }

    LowerCrane
        Entry{
            moveCrane(-4d);
            closeHand();
            startTimer(2000);
        }
    {
        evTimeout BackupToBridge { }
    }

    BackupToBridge
    Entry {
        linearDrive(-44d);
     }
    {
        evDriveComplete Complete {  }
        evDriveTimeout Complete {  }
    }

    Complete
    Entry {
    }
    {

    }
%%

/*
* Does a basic park from the bridge from teh quarry side.
* Assume the robot is starting with the side of the robot at
* the edge of the depot color
*/
%map BridgePark
%%
    Start
        Entry {
            startTimer(15000);
        }
    {
        evTimeout Drive { }
    }
    /*
    * drive forward 15 inches
    */
    Drive
        Entry {
            openHook();
            closeHand();
            linearDrive(18);
        }
    {
        evDriveComplete [ctxt.isBlueAlliance() == true] RotateToBridge {
            // Rotate CCW toward the bridge
            rotate(+90);
        }
        evDriveComplete [ctxt.isBlueAlliance() == false] RotateToBridge {
            // Rotate CW toward the bridge
            rotate(-90);
        }
        evDriveTimeout [ctxt.isBlueAlliance() == true] RotateToBridge {
            // Rotate CCW toward the bridge
            rotate(+90);
        }
        evDriveTimeout [ctxt.isBlueAlliance() == false] RotateToBridge {
            // Rotate CW toward the bridge
            rotate(-90);
        }
   }
    RotateToBridge
    {
        evRotationComplete nil { linearDriveBugUnblocker(); }
        // May not have completed but give it a try anyway
        evRotationTimeout nil {  linearDriveBugUnblocker(); }
        evDriveComplete DriveToBridge { linearDriveSlow(44d); }
        evDriveTimeout DriveToBridge { linearDriveSlow(44d); }
    }
    DriveToBridge
    {
        evDriveComplete Complete { }
        evDriveTimeout Complete { }
    }

    Complete
        Entry {
            stop();
        }
    {
    }

%%