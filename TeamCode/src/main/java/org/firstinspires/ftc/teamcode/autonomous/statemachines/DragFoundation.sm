%class DragFoundationController
%package org.firstinspires.ftc.teamcode.autonomous

%start DragFoundation::Idle

%map DragFoundation
%%

    /*
    * idle state until started by controller
    */
    Idle
    {
        evStart DriveForward {}
    }


    /*
    * drives forward toward foundation and (hopefully) stops at foundation.
    */
    DriveForward
        Entry {
            setLogMessage("DriveForward");
            openHook();
            // start drive forward
            driveToFoundation();
        }
    {
        evDriveComplete DriveForwardSuccess {}
        evDriveFail DriveForwardFail {}
    }


    /*
    * Stop after drive forward and check to see if we are at foundation.
    */
    DriveForwardSuccess
        Entry{
            setLogMessage("DriveForwardSuccess");
            // close the hook and wait for it stop
            closeHook();
            startHookTimer();
        }
    {
        evHookTimeout DragFoundation { }
    }
    /*
    * Stop after drive forward and check to see if we are at foundation.
    */
    DriveForwardFail
        Entry{
            setLogMessage("DriveForwardFail");
            // TODO Try to get location and correct
        }
    {

    }

    /*
    * Drag the foundation back
    */
    DragFoundation
        Entry {
            setLogMessage("DragFoundation");
            // pull the foundation backward
            dragFoundation();
        }
    {
        evDriveComplete nil {
            openHook();
            startHookTimer();
        }
        evHookTimeout RotateToQuarry {}
    }

    /*
    * Rotate the robot to drive toward the quarry
    */
    RotateToQuarry
        Entry {
            setLogMessage("RotateToQuarry");
            rotateToQuarry();
        }
    {
        evRotationComplete PrepareDriveToQuarry { }
    }

    PrepareDriveToQuarry
    Entry {
        // Retract Arm so that we can fit under the bridge before the arm and wait here until
        // it is retracted.
        setLogMessage("PrepareDriveToQuarry");
        retractArm();
    }
    {
        evArmRetracted DriveToQuarry {}
    }

    /*
    * drive to quarry.
    */
    DriveToQuarry
    Entry {
        setLogMessage("DriveToQuarry");
        driveToQuarry();
    }
    {
        evDriveComplete Success {}
    }

    /*
    * rotate toward the block at the quarry.
    */
    RotateToBlock
    Entry{
        setLogMessage("RotateToBlock");
    }
    {
    }

    /*
    * Final success state
    */
    Success
        Entry{
            setLogMessage("Success");
        }
    {

    }
%%

