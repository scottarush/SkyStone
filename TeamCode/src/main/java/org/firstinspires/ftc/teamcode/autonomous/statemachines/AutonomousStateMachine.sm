%class AutonomousController
%package org.firstinspires.ftc.teamcode.autonomous

%start AutonomousStateMachine::Idle

%map AutonomousStateMachine
%%

    /*
    * idle state until started by controller
    */
    Idle
    {
        evStart DriveToStones {}
    }


    /*
    * drives forward close enough to be able to locate a Skystone with the camera
    */
    DriveToStones
        Entry {
            openHook();
            // start drive forward
            linearDrive(10d);
        }
    {
        evDriveComplete nil { checkStoneRecognition(); }
        evDriveFail DriveForwardToIntake {}
        evSkystoneFound StrafeToStone { strafeToSkystone(10d,5000); }
        evStoneFound StrafeToStone { strafeToSkystone(10d,5000); }
        evNoStoneFound DriveForwardToIntake {   }
    }

    StrafeToStone
        Entry{
         }
    {
        evDriveComplete DriveForwardToIntake {}
    }

    DriveForwardToIntake
        Entry{
            linearDrive(10d);
        }
    {
        evDriveComplete IntakeStone {}
    }
    /*
    * Stop after drive forward and check to see if we are at foundation.
    */
    IntakeStone
        Entry{
            // close the hook and wait for it to stop so that we grab the block
            closeHook();
            startHookTimer();
            // Also start the grabber to suck the block in for a short time
            startGrabber(true,1000);
        }
    {
        evHookTimeout BackupToBeginDrag { }
    }
    /*
    * Backup to begin the drag
    */
    BackupToBeginDrag
        Entry{
            linearDrive(-18d);
         }
    {
        evDriveComplete [ctxt.isBlueAlliance() == true] BlueAllianceRotateTowardBridge { }
        evDriveComplete [ctxt.isBlueAlliance() == false] RedAllianceRotateTowardBridge { }
    }

    /*
    *
    */
    BlueAllianceRotateTowardBridge
        Entry{
            // Rotate CCW toward the bridge
            rotate(+90);
        }
    {
        evRotationComplete DragStone { }
    }
    /*
    *
    */
    RedAllianceRotateTowardBridge
        Entry{
            // Rotate CW toward the bridege
            rotate(-90);
         }
    {
        evRotationComplete DragStone { }
    }
    /*
    * Drag the stone back
    */
    DragStone
        Entry {
            linearDrive(72d);
        }
    {
        evDriveComplete ReleaseStone {}
    }

    /*
    *
    */
    ReleaseStone
        Entry {
            openHook();
            startGrabber(false,3000);
            linearDrive(-37d);
        }
    {

        evDriveComplete Complete { }
    }
    /*
    * Final success state
    */
    Complete
        Entry{
            stopGrabber();
        }
    {

    }
%%

