%class AutonomousController
%package org.firstinspires.ftc.teamcode.autonomous

%start AutonomousStateMachine::Idle

%map AutonomousStateMachine
%%

    /*
    * idle state until started by controller
    */
    Idle
    {
        evStart DriveToStones {}
    }


    /*
    * drives forward up to the stones
    */
    DriveToStones
        Entry {
            openHook();
            // start drive forward
            linearDrive(30d);
        }
    {
        evDriveComplete LocateSkystone {}
 //       evDriveFail DriveToStoneFailure {}
    }

    /*
    * Locates a skystone by side strafe
    */
    LocateSkystone
        Entry{
            checkForStones();
        }
    {
        evSkystoneFound StrafeToStone { strafeDriveToSkystone(); }
        evStoneFound StrafeToStone { strafeDriveToStone(); }
        evNoStoneFound DriveForwardToIntake {}
    }

    StrafeToStone
        Entry{
         }
    {
        evDriveComplete DriveForwardToIntake {}
    }

    DriveForwardToIntake
        Entry{
            linearDrive(6d);
        }
    {
        evDriveComplete IntakeStone {}
    }
    /*
    * Stop after drive forward and check to see if we are at foundation.
    */
    IntakeStone
        Entry{
            // close the hook and wait for it to stop so that we grab the block
            closeHook();
            startHookTimer();
            // Also start the grabber to suck the block in for a short time
            startGrabber(true,1000);
        }
    {
        evHookTimeout BackupToBeginDrag { }
    }
    /*
    * Backup to begin the drag
    */
    BackupToBeginDrag
        Entry{
            linearDrive(-18d);
         }
    {
        evDriveComplete [ctxt.isBlueAlliance() == true] BlueAllianceRotateTowardBridge { }
        evDriveComplete [ctxt.isBlueAlliance() == false] RedAllianceRotateTowardBridge { }
    }

    /*
    *
    */
    BlueAllianceRotateTowardBridge
        Entry{
            rotate(-90);
        }
    {
        evRotationComplete DragStone { }
    }
    /*
    *
    */
    RedAllianceRotateTowardBridge
        Entry{
            rotate(+90);
         }
    {
        evRotationComplete DragStone { }
    }
    /*
    * Drag the stone back
    */
    DragStone
        Entry {
            linearDrive(72d);
        }
    {
        evDriveComplete ReleaseStone {}
    }

    /*
    *
    */
    ReleaseStone
        Entry {
            openHook();
            startGrabber(false,3000);
            linearDrive(-37d);
        }
    {

        evDriveComplete Complete { }
    }
    /*
    * Final success state
    */
    Complete
        Entry{
            stopGrabber();
        }
    {

    }
%%

