%class AutonomousController
%package org.firstinspires.ftc.teamcode.autonomous

%start AutonomousStateMachine::Idle

%map AutonomousStateMachine
%%

    /*
    * idle state until started by controller
    */
    Idle
    {
        evStart DriveToStones {}
    }


    /*
    * drives forward up to the stones
    */
    DriveToStones
        Entry {
            setLogMessage("DriveToStones");
            openHook();
            // start drive forward
            linearDrive(33d);
        }
    {
        evDriveComplete DriveToStoneSuccess {}
 //       evDriveFail DriveToStoneFailure {}
    }


    /*
    * Stop after drive forward and check to see if we are at foundation.
    */
    DriveToStoneSuccess
        Entry{
            setLogMessage("DriveToStoneSuccess");
            // close the hook and wait for it to stop so that we grab the block
            closeHook();

            startHookTimer();
        }
    {
        evHookTimeout BackupToBeginDrag { }
    }
    /*
    * Backup to begin the drag
    */
    BackupToBeginDrag
        Entry{
            setLogMessage("BackupToBeginDrag");
            linearDrive(-18d);
         }
    {
        evDriveComplete [ctxt.isBlueAlliance() == true] BlueAllianceRotateTowardBridge { }
        evDriveComplete [ctxt.isBlueAlliance() == false] RedAllianceRotateTowardBridge { }
    }

    /*
    *
    */
    BlueAllianceRotateTowardBridge
        Entry{
            setLogMessage("BlueAllianceRotateTowardBridge");
            rotate(-90);
        }
    {
        evRotationComplete DragStone { }
    }
    /*
    *
    */
    RedAllianceRotateTowardBridge
        Entry{
             setLogMessage("RedAllianceRotateTowardBridge");
            rotate(+90);
         }
    {
        evRotationComplete DragStone { }
    }
    /*
    * Drag the stone back
    */
    DragStone
        Entry {
            setLogMessage("DragStone");
            linearDrive(72d);
        }
    {
        evDriveComplete ReleaseStone {}
    }

    /*
    *
    */
    ReleaseStone
        Entry {
            setLogMessage("ReleaseStone");
            openHook();
            startGrabber(false,3000);
            linearDrive(-37d);
        }
    {

        evDriveComplete Complete { }
    }
    /*
    * Final success state
    */
    Complete
        Entry{
            setLogMessage("Complete");
            stopGrabber();
        }
    {

    }
%%

